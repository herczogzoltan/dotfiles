# Credits to Zack Holman - https://github.com/holman/dotfiles/blob/7b8b643640e636d499fc4eaaf4de57d5bd8c2261/zsh/zshrc.symlink
# You may find some modifications according to the linked code above.

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# In case you want to have machine-specific configuration
# which will be excluded from versioning.
if [[ -e "$HOME/.local.zsh" ]]; then
	source $HOME/.local.zsh
fi


# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

unset config_files
